constraint VerkehrConstraint oftype AllowlistConstraint {
    allowlist: ["FV", "RV", "nur DPN"];
}

valuetype VerkehrType oftype text {
	constraints: [
		VerkehrConstraint,
	];
}


constraint CoordinateConstraint oftype RangeConstraint {
    lowerBound: -90;
    upperBound: 90;
}

valuetype Coordinate oftype decimal {
    constraints: [
        CoordinateConstraint,
    ];
}


constraint IfoptConstraint oftype RegexConstraint {
    regex: /[a-z]{2}:\d+:\d+(:\d+)?(:\d+)?/;
}

valuetype IFOPT oftype text {
    constraints: [
        IfoptConstraint,
    ];
}


pipeline TrainstopsPipeline {
    TrainstopsExtractor 
        -> TrainstopsTextFileInterpreter
        -> TrainstopsCsvInterpreter
        -> TrainstopsTableInterpreter
        -> TrainstopsSQLiteLoader;

        
    block TrainstopsExtractor oftype HttpExtractor {
        url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";
    }

    block TrainstopsTextFileInterpreter oftype TextFileInterpreter {

    }

    block TrainstopsCsvInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block TrainstopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "EVA_NR" oftype integer,
			"DS100" oftype text,
			"IFOPT" oftype IFOPT,
			"NAME" oftype text,
			"Verkehr" oftype VerkehrType,
			"Laenge" oftype Coordinate,
			"Breite" oftype Coordinate,
			"Betreiber_Name" oftype text,
			"Betreiber_Nr" oftype integer
        ];
    }

    block TrainstopsSQLiteLoader oftype SQLiteLoader {
		table: "trainstops";
		file: "trainstops.sqlite";
	}
}