transform CelsiusToFahrenheit {
    from Celsius oftype decimal;
    to Fahrenheit oftype decimal;

    Fahrenheit: (Celsius * 9) / 5 + 32 ;
}

valuetype PositiveInteger oftype integer {
    constraints: [ PositiveInteger ];
}

constraint PositiveInteger oftype RangeConstraint {
    lowerBound: 0;      
}


pipeline TemperaturesPipeline {
    DataExtractor
        ->ZipArchiveInterpreter
        ->DataFilePicker
        ->DataTextFileInterpreter
        ->DataCSVInterpreter
        ->ColumnRangeSelector
        ->HeaderWriter1
        ->HeaderWriter2
        ->TemperaturTableInterpreter
        ->TemperaturConverter
        ->BatterieTemperaturConverter
        ->SQLLoader;

    block DataExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block DataFilePicker oftype FilePicker{
        path: "/data.csv";
    }

    block DataTextFileInterpreter oftype TextFileInterpreter {}

    block DataCSVInterpreter oftype CSVInterpreter { delimiter: ";"; }

    block ColumnRangeSelector oftype CellRangeSelector {
        select: range A1:K*;
    }

    block HeaderWriter1 oftype CellWriter {
        at: cell E1;
        write: ["Temperatur"];
    }

    block HeaderWriter2 oftype CellWriter {
        at: cell J1;
        write: ["Batterietemperatur"];
    }

    block TemperaturTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Geraet" oftype PositiveInteger,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype integer,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text
        ];
    }

    block TemperaturConverter oftype TableTransformer{
        inputColumn: "Temperatur";
        outputColumn:"Temperatur";
        use: CelsiusToFahrenheit;
    }   

    block BatterieTemperaturConverter oftype TableTransformer {
        inputColumn: "Batterietemperatur";
        outputColumn:"Batterietemperatur";
        use: CelsiusToFahrenheit;
    }

    block SQLLoader oftype SQLiteLoader { 
        table: "temperatures"; 
        file: "temperatures.sqlite"; 
    }
}